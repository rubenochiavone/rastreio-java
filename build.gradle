plugins {
    // Apply the java-library plugin to add support for Java Library
    id 'java-library'
    // Apply JaCoCo plugin to get code coverage report
    id 'jacoco'
    // Apply Maven and JFrog Bintray plugins to publish project
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

repositories {
    // Use jcenter for resolving your dependencies.
    // You can declare any Maven/Ivy/file repository here.
    jcenter()
}

dependencies {
    implementation 'com.squareup.okhttp3:okhttp:4.3.1'
    implementation 'org.jsoup:jsoup:1.12.1'

    // Use JUnit test framework
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.okhttp3:mockwebserver:4.4.0'
}

test {
    if (project.hasProperty('excludeTests')) {
        exclude project.property('excludeTests')
    }

    if (project.hasProperty('includeTests')) {
        include project.property('includeTests')
    }
}

// Code coverage configuration
jacoco {
    toolVersion = "0.8.5"
    reportsDir = file("$buildDir/customJacocoReportDir")
}

// Code coverage task
jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/jacocoHtml")
    }
}

// Release artifacts
tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    classifier 'sources'
}

tasks.register('javadocJar', Jar) {
    from javadoc
    classifier 'javadoc'
}

// POM configuration
def pomConfig = {
    licenses {
        license {
            name "The Apache Software License, Version 2.0"
            url "http://www.apache.org/licenses/LICENSE-2.0.txt"
            distribution "repo"
        }
    }
    developers {
        developer {
            name "Ruben Oliveira Chiavone"
            email "rubenochiavone@gmail.com"
        }
    }
    
    scm {
       url "https://github.com/rubenochiavone/rastreio-java"
    }
}

// Publishing task
publishing {
    publications {
        Publication(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId 'java.rastreio'
            artifactId 'rastreio'
            version '1.0.3'
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A Java library for tracking Correios\' shipments. Heavily inspired by https://github.com/talesluna/rastrojs')
                root.appendNode('name', 'rastreio-java')
                root.appendNode('url', 'https://github.com/rubenochiavone/rastreio-java')
                root.children().last() + pomConfig
            }
        }
    }
}

// JFrog Bintray configuration
bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['Publication']
    pkg {
        repo = 'rastreio'
        name = 'rastreio-java'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/rubenochiavone/rastreio-java.git'
        version {
            name = '1.0.3'
        }
    }
}
